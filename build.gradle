buildscript {
    ext { springBootVersion = '2.5.2'
	      hibernateVersion = '5.11.9.Final'
		  swaggerVersion = '3.0.0'
		 }
		repositories {
			mavenCentral()
			maven { url 'https://jitpack.io' }
		}
    dependencies { classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    			   classpath files("lib/gradle-js-plugin-2.14.2-SNAPSHOT.jar")     			   
    			  }
}




plugins {
  id 'org.springframework.boot' version "${springBootVersion}"
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'java'
  id "com.eriwen.gradle.js" version "2.14.1"
//  id "com.gorylenko.gradle-git-properties" version "2.2.4"
  id 'de.undercouch.download' version '4.1.2'
  id 'eclipse'
  id 'jacoco'
  id "org.sonarqube" version "3.3"
}

sonarqube {
  properties {
    property "sonar.projectKey", "ShioCMS_shio"
    property "sonar.organization", "shiocms"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

jar {
    archiveBaseName = 'viglet-shio'
    archiveVersion =  '0.3.8'
}

eclipse {
    classpath {
        file {
            whenMerged {
                def src = entries.find { it.path == 'src/main/resources' }
                src.excludes = ['**']
            }
        }
    }

}

// gitProperties {
//    extProperty = 'gitProps' // git properties will be put in a map at project.ext.gitProps
// }

bootJar {
	launchScript() // executable jar
//    manifest {
//    	attributes(    
//        'Implementation-Version': "${-> project.jar.version + '-' + project.ext.gitProps['git.commit.id.abbrev']}"
//    	)
//  	} 
}

 bootRun {
   jvmArgs = ["--add-modules", "java.se", 
  "--add-exports", "java.base/jdk.internal.ref=ALL-UNNAMED",
  "--add-opens", "java.base/java.lang=ALL-UNNAMED",
  "--add-opens", "java.base/java.nio=ALL-UNNAMED",
  "--add-opens", "java.base/sun.nio.ch=ALL-UNNAMED",
  "--add-opens", "java.management/sun.management=ALL-UNNAMED",
  "--add-opens", "jdk.management/com.sun.management.internal=ALL-UNNAMED"]
}
repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

group = 'com.viglet.shio'
sourceCompatibility = 14
targetCompatibility = 14

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

task jsdocjs(type: com.eriwen.gradle.js.tasks.JsDocTask) {
    source = ["src/main/resources/js/server-side"]
    destinationDir = file("docs/javascript")
}

// Combine JS files
task combineSystemJs(type: com.eriwen.gradle.js.tasks.CombineJsTask) {
    source = "${projectDir}/src/main/resources/ui/js/src/system"
    dest = "${projectDir}/src/main/resources/ui/js/build/shSystem.js"
}

task combineFeatureJs(dependsOn: ':combineSystemJs', type: com.eriwen.gradle.js.tasks.CombineJsTask) {
    source = "${projectDir}/src/main/resources/ui/js/src/feature"
    dest = "${projectDir}/src/main/resources/ui/js/build/shFeature.js"
}

task combineAllJs(dependsOn: ':combineFeatureJs', type: com.eriwen.gradle.js.tasks.CombineJsTask) {
    source = ["${projectDir}/src/main/resources/ui/js/src/main/__shFactory.js", "${projectDir}/src/main/resources/ui/js/build/shSystem.js", "${projectDir}/src/main/resources/ui/js/build/shFeature.js"]
    dest = "${projectDir}/src/main/resources/ui/js/build/shio.js"
}

task minifyAllJs(dependsOn: ':combineAllJs', type: com.eriwen.gradle.js.tasks.MinifyJsTask) {
 	source = "${projectDir}/src/main/resources/ui/js/build/shio.js"
    dest = file("${projectDir}/src/main/resources/ui/public/js/dist/shio.min.js")
    closure {
		compilerOptions.languageIn="ECMASCRIPT6"
		compilerOptions.languageOut="ECMASCRIPT5"
        warningLevel = 'QUIET'
    }
}

task graphQLDocs(type:Exec) {
commandLine "graphdoc","--force", "-e",  "http://localhost:2710/graphql", "-o", "./docs/graphql"
}

task stage(dependsOn: ['build', 'clean'])

compileJava.dependsOn(['minifyAllJs'])

build.mustRunAfter clean

dependencies {
    
    // Spring Boot
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "${springBootVersion}"    
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: "${springBootVersion}"
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-parent', version:"${springBootVersion}"
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security', version:"${springBootVersion}"
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-activemq', version:"${springBootVersion}"   
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf', version:"${springBootVersion}"
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version:"${springBootVersion}"
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache', version:"${springBootVersion}"
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-mail', version:"${springBootVersion}"
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json', version:"${springBootVersion}"

	// Viglet Turing
	implementation 'com.github.openturing:turing-java-sdk:0.3.3'

  	// Spring
  	implementation group: 'org.springframework', name: 'spring-test', version: '5.3.8'
  	    
  	// Jackson
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-hibernate5', version: '2.12.4'
   
   	// Oracle	
	implementation group: 'com.oracle.ojdbc', name: 'ojdbc8', version: '19.3.0.0'
	
    // H2
    implementation group: 'com.h2database', name: 'h2', version:'1.4.200'

	// MariaDB
    implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.7.3'

    // Apache Commons
    implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.21'   
    implementation group: 'commons-io', name: 'commons-io', version:'2.11.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
	implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'
	
	//Zip4j
    implementation group: 'net.lingala.zip4j', name: 'zip4j', version: '2.9.0'

    // Apache POI
    implementation group: 'org.apache.poi', name: 'poi', version: '5.0.0'
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.0.0'
    
    // JSoup
	implementation group: 'org.jsoup', name: 'jsoup', version: '1.14.1'
	
	// Hibernate
	implementation group: 'org.hibernate', name: 'hibernate-search-engine', version: "${hibernateVersion}"
	implementation group: 'org.hibernate', name: 'hibernate-search-orm', version: "${hibernateVersion}"
	
	//Hazelcast
	implementation group: 'com.hazelcast', name: 'hazelcast-all', version : '4.0.3'	
	implementation group: 'com.hazelcast', name: 'hazelcast-spring', version: '4.0.3'
	
	
	// Swagger 2
	implementation group: 'io.springfox', name: 'springfox-swagger2', version: "${swaggerVersion}"
	implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: "${swaggerVersion}"
	
	// Gson - is used by reCAPTCHA
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.7'

	// Thumbnailator
    implementation group: 'net.coobird', name: 'thumbnailator', version: '0.4.14'
	
	// Tika
    implementation group: 'org.apache.tika', name: 'tika-core', version:'1.27'
    implementation group: 'org.apache.tika', name: 'tika-parsers', version:'1.27'
    
	// LDAP
	implementation group: 'ldapsdk', name: 'ldapsdk', version: '4.1'
	
	// Git 
	implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.12.0.202106070339-r'
		
	// GraphQL
	implementation 'com.graphql-java:graphql-java:230521-nf-execution'
	implementation 'com.graphql-java:graphql-java-spring-webmvc:2021-03-29T16-31-33-ae4bd96'	
	implementation 'com.graphql-java:graphql-java-extended-scalars:16.0.1'
	implementation 'io.projectreactor:reactor-core:3.4.8'
	implementation "com.graphql-java-kickstart:graphql-webclient-spring-boot-starter:1.0.0"

	// Color Thief
	implementation group: 'de.androidpit', name: 'color-thief', version: '1.1.2'

	// Guava
	implementation 'com.google.guava:guava:30.1.1-jre'
	
	// WebJars
	implementation 'org.webjars:jquery:1.12.4'
	implementation 'org.webjars.bower:jquery.ui:1.12.1'
	implementation 'org.webjars:bootstrap:3.3.7-1' // Content
	implementation 'org.webjars.bower:bootstrap:4.6.0' // Preview
	implementation 'org.webjars.npm:angular:1.8.2'
	implementation 'org.webjars.npm:angular-resource:1.8.2'
	implementation 'org.webjars.npm:angular-animate:1.8.2'
	implementation 'org.webjars.npm:angular-sanitize:1.8.2'	
	implementation 'org.webjars.npm:angular-cookies:1.8.2'
	implementation 'org.webjars.bower:ace-builds:1.4.12'
	implementation 'org.webjars.bower:tinymce:5.8.2'
	implementation 'org.webjars.bower:angular-ui-router:1.0.28'
	implementation 'org.webjars.npm:angular-translate:2.18.4'		
	implementation 'org.webjars.bower:angular-bootstrap:2.5.0'
	implementation 'org.webjars.bower:angular-moment:1.2.0'
	implementation 'org.webjars.bower:angular-ui-ace:0.2.3'
	implementation 'org.webjars.bower:font-awesome:4.7.0'
	implementation 'org.webjars.bower:ng-file-upload:12.2.13'
	implementation 'org.webjars.bower:angular-ui-notification:0.3.6'
	implementation 'org.webjars.bower:angular-ui-tinymce:0.0.19'
	implementation 'org.webjars.bower:angular-ui-sortable:0.19.0'
	implementation 'org.webjars.bower:angular-ui-select:0.19.8'
	implementation 'org.webjars.bower:ngInfiniteScroll:1.3.4'
	implementation 'org.webjars:angular-avatar:1.3.0'
	implementation 'org.webjars.bower:angular-bootstrap-toggle:0.4.2'

	testImplementation('org.springframework.boot:spring-boot-starter-test')
}

test {
	useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
    
 	jacocoTestReport {
    	reports {
        	xml.enabled true
        	html.enabled false
    	}
	}
   
	check.dependsOn jacocoTestReport   

}
